import java.nio.file.Path
import java.nio.file.Paths

allprojects {
    apply plugin: FlutterAarPlugin
}

/** Adds in a cmpileOnly dependency on flutterJar. */
class FlutterAarPlugin implements Plugin<Project> {
    private File flutterJar
    private File debugFlutterJar
    private File releaseFlutterJar
    private Properties localProperties

    @Override
    void apply(Project project) {
        findFlutterJar(project)
        project.afterEvaluate this.&addFlutterJarCompileOnlyDependency
    }

    private Properties readPropertiesIfExist(File propertiesFile) {
        Properties result = new Properties()
        if (propertiesFile.exists()) {
            propertiesFile.withReader('UTF-8') { reader -> result.load(reader) }
        }
        return result
    }

    private String resolveProperty(Project project, String name, String defaultValue) {
        if (localProperties == null) {
            localProperties = readPropertiesIfExist(new File(project.projectDir.parentFile, "local.properties"))
        }
        String result
        if (project.hasProperty(name)) {
            result = project.property(name)
        }
        if (result == null) {
            result = localProperties.getProperty(name)
        }
        if (result == null) {
            result = defaultValue
        }
        return result
    }

    // TODO(mklim): This logic is copied from flutter.gradle. Should probably
    // come from the same source instead.
    private void findFlutterJar(Project project) {
        if (project.hasProperty('localEngineOut')) {
            String engineOutPath = project.property('localEngineOut')
            File engineOut = project.file(engineOutPath)
            if (!engineOut.isDirectory()) {
                throw new GradleException('localEngineOut must point to a local engine build')
            }
            flutterJar = Paths.get(engineOut.absolutePath, "flutter.jar").toFile()
            if (!flutterJar.isFile()) {
                throw new GradleException('Local engine build does not contain flutter.jar')
            }

            localEngine = engineOut.name
            localEngineSrcPath = engineOut.parentFile.parent

            project.dependencies {
                compileOnly project.files(flutterJar)
            }
        } else {
            String flutterRootPath = resolveProperty(project, "flutter.sdk", System.env.FLUTTER_ROOT)
            if (flutterRootPath == null) {
                throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file or with a FLUTTER_ROOT environment variable.")
            }
            File flutterRoot = project.file(flutterRootPath)
            if (!flutterRoot.isDirectory()) {
                throw new GradleException("flutter.sdk must point to the Flutter SDK directory")
            }
            Path baseEnginePath = Paths.get(flutterRoot.absolutePath, 'bin', 'cache', 'artifacts', 'engine')
            String targetArch = 'arm'
            if (project.hasProperty('target-platform') &&
                project.property('target-platform') == 'android-arm64') {
              targetArch = 'arm64'
            }
            debugFlutterJar = baseEnginePath.resolve("android-${targetArch}").resolve("flutter.jar").toFile()
            releaseFlutterJar = baseEnginePath.resolve("android-${targetArch}-release").resolve("flutter.jar").toFile()
            if (!debugFlutterJar.isFile()) {
                project.exec {
                    executable flutterExecutable.absolutePath
                    args "--suppress-analytics"
                    args "precache"
                }
                if (!debugFlutterJar.isFile()) {
                    throw new GradleException("Unable to find flutter.jar in SDK: ${debugFlutterJar}")
                }
            }
        }
    }

    private void addFlutterJarCompileOnlyDependency(Project project) {
        if (project.state.failure) {
            return
        }
        project.dependencies {
            if (flutterJar != null) {
                compileOnly project.files(flutterJar)
            } else {
                compileOnly project.files(releaseFlutterJar)
            }
        }
    }
}